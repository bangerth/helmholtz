set logsc x
set xlabel "Frequency f"
set key top left
set samples 1000

i = {0.0,1.0}

K=1
rho=1
c=sqrt(K/rho)
k(omega)=omega/c
p(omega)=sinh(i*k(omega)/2)/sinh(i*k(omega))

u(omega)=1./(i*rho)*cos(k(omega)/2)/sin(k(omega))

set term png

set output "pressure-at-center.png"
set ylabel "p(0.5)"
plot [0.01:4][-2:2] \
  "dii_frequency_response.csv" using 1:14 title "Computed real part", \
  "dii_frequency_response.csv" using 1:15 title "Computed imaginary part", \
  real(p(2*pi*x)) title "Theory real part", \
  imag(p(2*pi*x)) title "Theory imaginary part"



set output "velocity-at-center.png"
set ylabel "u(0.5)"
plot [0.01:4][-20:20] \
  "dii_frequency_response.csv" using 1:16 title "Computed real part", \
  "dii_frequency_response.csv" using 1:17 title "Computed imaginary part", \
  real(u(2*pi*x)) title "Theory real part", \
  imag(u(2*pi*x)) title "Theory imaginary part"
