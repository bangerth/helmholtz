set logsc x
set xlabel "Frequency f"
set key top left
set samples 1000

i = {0.0,1.0}

K=1
rho=1-i
c=sqrt(K/rho)
k(omega)=omega/c
L=0.01
p(omega)=sinh(i*k(omega)*L/2)/sinh(i*k(omega)*L)

u(omega)=1./sqrt(rho*K)*cosh(i*k(omega)*L/2)/sinh(i*k(omega)*L)

set term png

set output "pressure-at-center.png"
set ylabel "p(0.5)"
plot [1:400][-2:2] \
  "dii_frequency_response.csv" using 1:14 title "Computed real part", \
  "dii_frequency_response.csv" using 1:15 title "Computed imaginary part", \
  real(p(2*pi*x)) title "Theory real part", \
  imag(p(2*pi*x)) title "Theory imaginary part"



set output "velocity-at-center.png"
set ylabel "u(0.5)"
plot [1:400][-8:8] \
  "dii_frequency_response.csv" using 1:16 title "Computed real part", \
  "dii_frequency_response.csv" using 1:17 title "Computed imaginary part", \
  real(u(2*pi*x)) title "Theory real part", \
  imag(u(2*pi*x)) title "Theory imaginary part"
