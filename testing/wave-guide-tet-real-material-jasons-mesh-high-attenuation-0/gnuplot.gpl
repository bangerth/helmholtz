set logsc x
set xlabel "Frequency f"
set key top left
set samples 1000

i = {0.0,1.0}

K=101582+2796.01*i
rho=1.43257-872.485*i
c=sqrt(K/rho)
k(omega)=omega/c
L=0.01
p(omega)=sinh(i*k(omega)*L/2)/sinh(i*k(omega)*L)

u(omega)=1./sqrt(rho*K)*cosh(i*k(omega)*L/2)/sinh(i*k(omega)*L)
impedance(omega)=sqrt(rho*K)*tanh(i*k(omega)*L)


set term png


make_complex(rr,ii)=rr+ii*i

set output "impedance.png"
set ylabel "Impedance Z"
plot [][] \
  "dii_frequency_response.csv" using 1:(real(1./make_complex($2,$3))) title "Computed real part, original mesh", \
  "dii_frequency_response.csv" using 1:(imag(1./make_complex($2,$3))) title "Computed imaginary part, original mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-1/dii_frequency_response.csv" using 1:(real(1./make_complex($2,$3))) title "Computed real part, once refined mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-1/dii_frequency_response.csv" using 1:(imag(1./make_complex($2,$3))) title "Computed imaginary part, once refined mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-2/dii_frequency_response.csv" using 1:(real(1./make_complex($2,$3))) title "Computed real part, twice refined mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-2/dii_frequency_response.csv" using 1:(imag(1./make_complex($2,$3))) title "Computed imaginary part, twice refined mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-3/dii_frequency_response.csv" using 1:(real(1./make_complex($2,$3))) title "Computed real part, 3x refined mesh", \
  "../wave-guide-tet-real-material-jasons-mesh-high-attenuation-3/dii_frequency_response.csv" using 1:(imag(1./make_complex($2,$3))) title "Computed imaginary part, 3x refined mesh", \
  real(impedance(2*pi*x)) title "Theory real part", \
  imag(impedance(2*pi*x)) title "Theory imaginary part"


pressure(x,omega)=sinh(i*k(omega)*(L-x))/sinh(i*k(omega)*L)
unset logscale x
set key top right
set xlabel "x [meter]"
set ylabel "p(x) [Pa]"
omega=10000
set output "solution-10kHz.png"
plot [0:L] real(pressure(x,omega*2*pi)) title "Pressure, real part", \
           imag(pressure(x,omega*2*pi)) title "Pressure, real part"